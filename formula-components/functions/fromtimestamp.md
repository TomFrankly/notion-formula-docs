---
description: Learn how to use the fromTimestamp function in Notion formulas.
---

# fromTimestamp

The `fromTimestamp()` function converts a Unix timestamp into a [date](../../formula-basics/data-types/date-data-type.md).

{% code overflow="wrap" lineNumbers="true" %}
```jsx
fromTimestamp(number)
```
{% endcode %}

The Unix timestamp is the number of seconds since January 1, 1970 at 00:00 UTC. It is also known as Unix time or Epoch time.

Note that Notion’s formula editor specifically looks for a Unix **millisecond** timestamp.

This is important to remember, because `fromTimestamp(1656012840)` returns `January 20, 1970 4:00 AM` in Notion (in UTC - it’ll look different based on your timezone).

Unix timestamps are _normally_ expressed in **seconds,** so most epoch converters would express `1656012840` as `June 23, 2022 7:34 PM`.

In Notion, you’d need to write `fromTimestamp(1656012840000)` to get the same date.

You can use this tool to convert human-readable dates to timestamps, and vice-versa:

{% embed url="https://www.epochconverter.com/" %}

Learn more about Unix time:

{% embed url="https://www.howtogeek.com/759337/what-is-the-unix-epoch-and-how-does-unix-time-work/" %}

{% hint style="info" %}
**Good to know:** Notion will always show dates/times created using `fromTimestamp()` and [now](now.md) in your local time zone. You cannot specify another time zone (you _can_ do this with actual date properties).

[Learn more about how Notion handles date objects](../../formula-basics/data-types/date-data-type.md).
{% endhint %}

## Example Formula

{% code overflow="wrap" lineNumbers="true" %}
```jsx
// Notion will express this date in your local time zone, so it
// may look different if you try this formula out.hin
fromTimestamp(1656012840000) // Output: June 23, 2022 7:34 PM (UTC)
```
{% endcode %}

## Example Database

The example database below uses [timestamp](timestamp.md) and `fromTimestamp()` to take in a date and return a different, semi-randomized date.&#x20;

<details>

<summary>A note about this example's "randomness"</summary>

The “randomness” of this formula is very poor, as the formula has intentionally been kept simple so it can be easily understood.

If you're curious, [here's an article on pseudorandom number generation](https://en.wikipedia.org/wiki/Pseudorandom\_number\_generator) that explains the potential pitfalls with using computational algorithms to generate "random" numbers.

Notion does not provide any randomness functions within its formula editor. If you need to generate better random numbers, you should probably create a [Notion API integration](https://developers.notion.com/reference/intro) and utilize a function like [JavaScript's Math.random()](https://developer.mozilla.org/en-US/docs/Web/JavaScript/Reference/Global\_Objects/Math/random).

</details>

<figure><img src="../../.gitbook/assets/fromTimestamp Function - Notion Formulas.png" alt=""><figcaption></figcaption></figure>

### View and Duplicate Database

{% embed url="https://thomasfrank.notion.site/fromTimestamp-70432403aa624bb3941c653bfef98c70" %}

### "Random Date" Property Formula

{% code overflow="wrap" lineNumbers="true" %}
```jsx
// Compressed
if(minute(prop("Edited")) % 2 == 0, fromTimestamp(timestamp(prop("Date")) + toNumber(slice(replaceAll(id(), "\\D", ""), (timestamp(prop("Edited")) + 956348) % 7 - 3, 13))), fromTimestamp(timestamp(prop("Date")) - toNumber(slice(replaceAll(id(), "\\D", ""), (timestamp(prop("Edited")) + 891327) % 7 - 3, 13))))

// Edited
if(
    minute(
        prop("Edited")
    ) % 2 == 0,
    fromTimestamp(
        timestamp(
            prop("Date")
        ) +
        toNumber(
            slice(
                replaceAll(
                    id(), 
                    "\\D", 
                    ""
                ), 
                (timestamp(
                    prop("Edited")
                ) + 956348) % 7 - 3, 
                13
            )
        )
    ),
    fromTimestamp(
        timestamp(
            prop("Date")
        ) -
        toNumber(
            slice(
                replaceAll(
                    id(), 
                    "\\D", 
                    ""
                ), 
                (timestamp(
                    prop("Edited")
                ) + 891327) % 7 - 3, 
                13
            )
        )
    )
)
```
{% endcode %}

To return a seemingly random date, this formula takes the [timestamp](timestamp.md) of the Date property's date and manipulates it.

First, an [if statement](../operators/if.md) determines if the [minute](minute.md) of the input date is even or odd using the [mod](../operators/mod.md) function.

If it is even, then the random date will be _later_ than the input date. If odd, the random date will be _earlier_ than the input date.

The number that is either added to or subtracted from the input date's timestamp is generated by taking the following steps:

1. Get the page id using the [id](id.md) function.
2. Remove all non-numeric characters with [replaceAll](replaceall.md) (which searches for `\\D`, which is a special [regular expression character class](../../reference/regular-expressions-in-notion-formulas.md#character-classes) meaning "any non-numeric character").
3. [Slice](slice.md) the resulting string in order to make it smaller. The end index is always 13, but the beginning index is variable. It is determined by `(timestamp(prop("Edited")) + 956348) % 7 - 3`.
4. [Convert](../../reference/converting-data-types.md) the sliced [string](../../formula-basics/data-types/string.md) to a [number](../../formula-basics/data-types/number.md) using [toNumber](tonumber.md).

This formula doesn't use [now](now.md) to determine its output, as the goal is for the date to change whenever the row is edited. Using now would cause the date to change every minute.

#### Other formula components used in this example:

{% content-ref url="../operators/if.md" %}
[if.md](../operators/if.md)
{% endcontent-ref %}

{% content-ref url="timestamp.md" %}
[timestamp.md](timestamp.md)
{% endcontent-ref %}

{% content-ref url="../operators/mod.md" %}
[mod.md](../operators/mod.md)
{% endcontent-ref %}

{% content-ref url="../operators/add.md" %}
[add.md](../operators/add.md)
{% endcontent-ref %}

{% content-ref url="../operators/subtract.md" %}
[subtract.md](../operators/subtract.md)
{% endcontent-ref %}

{% content-ref url="replaceall.md" %}
[replaceall.md](replaceall.md)
{% endcontent-ref %}

{% content-ref url="slice.md" %}
[slice.md](slice.md)
{% endcontent-ref %}

{% content-ref url="tonumber.md" %}
[tonumber.md](tonumber.md)
{% endcontent-ref %}

{% content-ref url="../operators/equal.md" %}
[equal.md](../operators/equal.md)
{% endcontent-ref %}

{% content-ref url="id.md" %}
[id.md](id.md)
{% endcontent-ref %}

#### About the Author

<img src="../../.gitbook/assets/Notion Fundamentals with Thomas Frank - Avatar 2021 compressed (1).png" alt="" data-size="line"> My name is Thomas Frank, and I'm a [Notion-certified](https://www.credly.com/badges/95fae13a-17bf-4b4a-a3d2-d58c8a3e6a2a/public\_url) writer, YouTuber, and template creator. I've been using Notion since 2018 to organize my personal life and to run my business and YouTube channel. In addition to this formula reference, I've created a [free Notion course for beginners](https://thomasjfrank.com/fundamentals/) and several [productivity-focused Notion templates](https://thomasjfrank.com/templates/). If you'd like to connect, [follow me on Twitter](https://twitter.com/TomFrankly).
